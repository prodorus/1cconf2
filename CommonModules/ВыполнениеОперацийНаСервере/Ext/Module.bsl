// Выполняет проведение документов указанного вида на сервере 1С:Предприятия 
//
Процедура ПровестиДокументыНаСервере(ИмяДокумента) Экспорт
	
	Сообщить("Проведение документов " + ИмяДокумента + " --------------------");
	//Выберем все документы указаннго вида
	ВыборкаДокументов = Документы[ИмяДокумента].Выбрать();
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если ВыборкаДокументов.Проведен Тогда
		
			Сообщить("Проведение документа: " + ВыборкаДокументов);
			//Проводим документ
			ДокументОбъект = ВыборкаДокументов.ПолучитьОбъект();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		
		КонецЕсли;
		
	КонецЦикла
	 
КонецПроцедуры	

// Выполняет формирование движений приходной накладной на сервере 1С:Предприятия 
//
Процедура СформироватьДвиженияПриходнойНакладной(ДокументСсылка) Экспорт
	
		//Прочитаем данные документа
	    Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		               |	ПриходнаяНакладнаяСостав.Ссылка.Склад,
		               |	ПриходнаяНакладнаяСостав.Номенклатура,
		               |	ПриходнаяНакладнаяСостав.Количество,
		               |	ПриходнаяНакладнаяСостав.Ссылка.Дата
		               |ИЗ
		               |	Документ.ПриходнаяНакладная.Состав КАК ПриходнаяНакладнаяСостав
		               |
		               |ГДЕ
		               |	ПриходнаяНакладнаяСостав.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		НаборЗаписей = РегистрыНакопления.УчетНоменклатуры.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ДокументСсылка);
		
		//Выполним формирование движений
		Пока Выборка.Следующий() Цикл
			Движение = НаборЗаписей.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Выборка.Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Склад = Выборка.Склад;
			Движение.Количество = Выборка.Количество;
		КонецЦикла;
		
		НаборЗаписей.Записать();
	 
	КонецПроцедуры	
	
