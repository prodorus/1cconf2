// Процедура подготовки HTML страницы к отбражению
// Выгружает картинки во временные файлы,
// ссылки на картинки в документе делает абсолютными
//
Процедура РазвернутьHTMLСтраницу(Каталог, Текст, ФайлыКартинок) Экспорт
	
	// выгружаем картинки
	Для Каждого ФайлКартинки Из ФайлыКартинок Цикл
		ИмяФайла = Каталог + "/" + ФайлКартинки.Имя;
		Файл = Новый Файл(ИмяФайла);
		СоздатьКаталог(Файл.Путь);
		ФайлКартинки.Файл.Получить().Записать(ИмяФайла);
	КонецЦикла;
	
	// загружаем хтмл-документ, ищем в нем рисунки, ссылки делаем абсолютными
	HTMLДокумент = Новый COMОбъект("HtmlFile");
	HTMLДокумент.open("text/html");
	HTMLДокумент.write(Текст);
	HTMLДокумент.close();

	ImgCollection = HTMLДокумент.all.tags("img");
	Для i = 0 По ImgCollection.length - 1 Цикл
		ImgElement = ImgCollection.item(i);
		Src = ImgElement.src;
		Если Найти(Src, "about:blank") = 1 Тогда
			Src = Каталог + "/" + Сред(Src, 12);
			ImgElement.src = Src;
		КонецЕсли;
	КонецЦикла;
	
	// получаем измененный текст
	Текст = HTMLДокумент.all.tags("html").item(0).outerHTML;
	
КонецПроцедуры // РазвернутьHTMLСтраницу(Каталог, Текст, ФайлыКартинок)

// Процедура подготовки HTML страницы к записи
// Собирает картинки в таблицу значений, 
// ссылки на картинки в документе заменяет на уникальные идентификаторы
//
Процедура ПреобразоватьHTMLСтраницуПередЗаписью(Текст, ФайлыКартинок) Экспорт
	
	ФайлыКартинок.Очистить();
	
	// загружаем хтмл-документ, ищем в нем рисунки, сохраняем их в БД
	// сам документ преобразуем, заменив ссылки на картинки их временными именами
	HTMLДокумент = Новый COMОбъект("HtmlFile");
	HTMLДокумент.open("text/html");
	HTMLДокумент.write(Текст);
	HTMLДокумент.close();
	
	СохраненныеКартинки = Новый Соответствие;
	Номер = 1;
	
	ImgCollection = HTMLДокумент.all.tags("img");
	Для i = 0 По ImgCollection.length - 1 Цикл
		ImgElement = ImgCollection.item(i);
		Src = ImgElement.src;
		Если СохраненныеКартинки[Src] = Неопределено Тогда
			// эта картинка еще не скопирована
			
			// имя исходной картинки
			ИмяФайла = Src;
			Если Найти(ИмяФайла, "file:///") = 1 Тогда
				ИмяФайла = Сред(ИмяФайла, 9);
			ИначеЕсли Найти(ИмяФайла, "file:") = 1 Тогда
				ИмяФайла = Сред(ИмяФайла, 6);
			КонецЕсли;
			ИмяФайла = СтрЗаменить(ИмяФайла, "%20", " ");
			Файл = Новый Файл(ИмяФайла);
			
			// имя скопированной картинки
			СохраненныеКартинки[Src] = "image" + Номер + Файл.Расширение;
			Номер = Номер + 1;
			
			Картинка = ФайлыКартинок.Добавить();
			Картинка.Имя = СохраненныеКартинки[Src];
			Картинка.Файл = Новый ХранилищеЗначения(Новый ДвоичныеДанные(ИмяФайла));
		КонецЕсли;
		ImgElement.src = СохраненныеКартинки[Src];
	КонецЦикла;
	
	// получаем измененный текст
	Текст = HTMLДокумент.all.tags("html").item(0).outerHTML;
	
КонецПроцедуры // ПреобразоватьHTMLСтраницуПередЗаписью(Текст, ФайлыКартинок)

// Процедура сохранения HTML страницы вместе с картинками
//
Процедура СохранитьHTMLСтраницу(ИмяФайла, Текст, ФайлыКартинок) Экспорт
	
	Файл = Новый Файл(ИмяФайла);
	Каталог = Файл.ИмяБезРасширения + "_Files/";
	
	// загружаем хтмл-документ, ищем в нем рисунки, ссылки делаем абсолютными
	HTMLДокумент = Новый COMОбъект("HtmlFile");
	HTMLДокумент.open("text/html");
	HTMLДокумент.write(Текст);
	HTMLДокумент.close();

	ImgCollection = HTMLДокумент.all.tags("img");
	Для i = 0 По ImgCollection.length - 1 Цикл
		ImgElement = ImgCollection.item(i);
		Src = ImgElement.src;
		Если Найти(Src, "about:blank") = 1 Тогда
			Src = Каталог + Сред(Src, 12);
			ImgElement.src = Src;
		КонецЕсли;
	КонецЦикла;
	
	// получаем измененный текст и сохраняем его в файл
	НовыйФайл = Новый ЗаписьТекста;
	НовыйФайл.Открыть(ИмяФайла, КодировкаТекста.UTF8);
	НовыйФайл.ЗаписатьСтроку(HTMLДокумент.all.tags("html").item(0).outerHTML);
	
	// выгружаем картинки
	Каталог = Файл.Путь + "/" + Каталог;
	СоздатьКаталог(Каталог);
	Для каждого ФайлКартинки Из ФайлыКартинок Цикл
		ИмяФайла = Каталог + ФайлКартинки.Имя;
		Файл = Новый Файл(ИмяФайла);
		ФайлКартинки.Файл.Получить().Записать(ИмяФайла);
	КонецЦикла;

КонецПроцедуры // СохранитьHTMLСтраницу(ИмяФайла, Текст, ФайлыКартинок)
