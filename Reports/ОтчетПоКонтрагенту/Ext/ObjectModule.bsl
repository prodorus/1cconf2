Перем ОтборДатаС;
Перем ОтборДатаПо;

Процедура ЗаполнитьНачальныеНастройки() Экспорт

	ПостроительОтчета.Текст = "
	|ВЫБРАТЬ
	|	Документы.Ссылка КАК Документ,
	|	Документы.Ссылка.Представление КАК ДокументПредставление,
	|	Документы.СуммаПрих КАК СуммаПрих,
	|	Документы.СуммаРасх КАК СуммаРасх,
	|	Документы.Ссылка.Контрагент КАК Контрагент
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		РасходнаяНакладнаяСостав.Ссылка КАК Ссылка,
	|		Сумма(РасходнаяНакладнаяСостав.Сумма) КАК СуммаРасх,
	|		0 КАК СуммаПрих
	|	ИЗ
	|		Документ.РасходнаяНакладная.Состав КАК РасходнаяНакладнаяСостав
	|		
	|	ГДЕ
	|		РасходнаяНакладнаяСостав.Ссылка.Контрагент = &ВыбКонтрагент
	|		{ГДЕ РасходнаяНакладнаяСостав.Ссылка.Дата КАК Дата}
	|
	|	СГРУППИРОВАТЬ ПО
	|		РасходнаяНакладнаяСостав.Ссылка
	|		
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ПриходнаяНакладнаяСостав.Ссылка,
	|		0,
	|		Сумма(ПриходнаяНакладнаяСостав.Сумма)
	|	ИЗ
	|		Документ.ПриходнаяНакладная.Состав КАК ПриходнаяНакладнаяСостав
	|	
	|	ГДЕ
	|		ПриходнаяНакладнаяСостав.Ссылка.Контрагент = &ВыбКонтрагент
	|		{ГДЕ ПриходнаяНакладнаяСостав.Ссылка.Дата КАК Дата}
	|
	|	СГРУППИРОВАТЬ ПО
	|		ПриходнаяНакладнаяСостав.Ссылка
	|		
	|	) КАК Документы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документы.Ссылка.Дата ВОЗР
	|	
	|ИТОГИ СУММА(СуммаРасх),СУММА(СуммаПрих) ПО
	|	Общие
	|";
	
	ПостроительОтчета.РазмещениеИзмеренийВСтроках  = ТипРазмещенияИзмерений.Вместе;
	ПостроительОтчета.РазмещениеРеквизитовИзмеренийВСтроках = ТипРазмещенияРеквизитовИзмерений.ВместеСИзмерениями;
	ПостроительОтчета.ВыводитьДетальныеЗаписи = Истина;
	ПостроительОтчета.Макет = ЭтотОбъект.ПолучитьМакет("Макет");
	ПостроительОтчета.ЗаполнениеРасшифровки = ВидЗаполненияРасшифровкиПостроителяОтчета.ЗначенияГруппировок;
	ОтборДатаС = ПостроительОтчета.Отбор.Добавить("Дата");
	ОтборДатаС.ВидСравнения = ВидСравнения.БольшеИлиРавно;
	ОтборДатаПо = ПостроительОтчета.Отбор.Добавить("Дата");
	ОтборДатаПо.ВидСравнения = ВидСравнения.МеньшеИлиРавно;

КонецПроцедуры

Процедура СформироватьОтчет(Таб) Экспорт

	Таб.Очистить();
	
	ОтборДатаС.Значение = ДатаНачала;
	ОтборДатаС.Использование = ДатаНачала <> '00010101000000';
	ОтборДатаПо.Значение = КонецДня(ДатаОкончания);
	ОтборДатаПо.Использование = ДатаОкончания <> '00010101000000';

	ПостроительОтчета.Параметры.Вставить("ВыбКонтрагент", ВыбКонтрагент);
	
	Если ВыбКонтрагент.Пустая() Тогда
		ПостроительОтчета.Макет.Параметры.ШапкаОтчета = "Документы по контрагенту <...>";
	Иначе
		ПостроительОтчета.Макет.Параметры.ШапкаОтчета = "Документы по контрагенту """ + СокрЛП(ВыбКонтрагент) + """";
	КонецЕсли;
	
	ПостроительОтчета.Макет.Параметры.ШапкаОтчета = ПостроительОтчета.Макет.Параметры.ШапкаОтчета + " с " + ?(ДатаНачала = '00010101', "<...>", Формат(ДатаНачала, "ДФ=dd.MM.yyyy")) + " по " + ?(ДатаОкончания = '00010101', "<...>", Формат(ДатаОкончания, "ДФ=dd.MM.yyyy"));
	
	ПостроительОтчета.Выполнить();
	ПостроительОтчета.Вывести(Таб);
	
	Таб.ТолькоПросмотр = Истина;

КонецПроцедуры
