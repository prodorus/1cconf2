////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ
// 

// Процедура обработчик ПередЗаписью документа
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения) 
	
	// Определить нужно ли обновлять дату в движениях
	ОбновитьДатуДвижений = ЭтоНовый() Или Движения.УчетНоменклатуры.Модифицированность(); 
	Если Не ОбновитьДатуДвижений Тогда 
		
		// Проверить, что дата изменилась 
		Запрос = Новый Запрос; 
		Запрос.УстановитьПараметр("ТекущийДокумент", Ссылка); 
		Запрос.Текст = 
		"ВЫБРАТЬ 
		| Дата 
		|ИЗ 
		| Документ.ВводНачальныхОстатков 
		|ГДЕ Ссылка = &ТекущийДокумент"; 
				
		Выборка = Запрос.Выполнить().Выбрать(); 
		Выборка.Следующий(); 
		ОбновитьДатуДвижений = Выборка.Дата <> Дата; 
	КонецЕсли; 
	
	// Установить всем новую дату, если нужно 
	Если ОбновитьДатуДвижений Тогда 
		Если Не Движения.УчетНоменклатуры.Выбран() И Не Движения.УчетНоменклатуры.Модифицированность() Тогда 
			Движения.УчетНоменклатуры.Прочитать(); 
		КонецЕсли; 
		Для Каждого ЗаписьРегистра Из Движения.УчетНоменклатуры Цикл 
			ЗаписьРегистра.Период = Дата; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

